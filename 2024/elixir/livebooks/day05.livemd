<!-- livebook:{"autosave_interval_s":30} -->

# Advent of Code 2024 - Day 5

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1"}
])
```

## Introduction

### --- Day 5: Print Queue ---

Satisfied with their search on Ceres, the squadron of scholars suggests subsequently scanning the stationery stacks of sub-basement 17.

The North Pole printing department is busier than ever this close to Christmas, and while The Historians continue their search of this historically significant facility, an Elf operating a [very familiar printer](https://adventofcode.com/2017/day/1) beckons you over.

The Elf must recognize you, because they waste no time explaining that the new **sleigh launch safety manual** updates won't print correctly. Failure to update the safety manuals would be dire indeed, so you offer your services.

Safety protocols clearly indicate that new pages for the safety manuals must be printed in a **very specific order**. The notation `X|Y` means that if both page number `X` and page number `Y` are to be produced as part of an update, page number `X` **must** be printed at some point before page number `Y`.

The Elf has for you both the **page ordering rules** and the **pages to produce in each update** (your puzzle input), but can't figure out whether each update has the pages in the right order.

For example:

```
47|53
97|13
97|61
97|47
75|29
61|13
75|53
29|13
97|29
53|29
61|53
97|53
61|29
47|13
75|47
97|75
47|61
75|61
47|29
75|13
53|13

75,47,61,53,29
97,61,53,29,13
75,29,13
75,97,47,61,53
61,13,29
97,13,75,29,47
```

The first section specifies the **page ordering rules**, one per line. The first rule, `47|53`, means that if an update includes both page number 47 and page number 53, then page number 47 **must** be printed at some point before page number 53. (47 doesn't necessarily need to be **immediately** before 53; other pages are allowed to be between them.)

The second section specifies the page numbers of each **update**. Because most safety manuals are different, the pages needed in the updates are different too. The first update, `75,47,61,53,29`, means that the update consists of page numbers 75, 47, 61, 53, and 29.

To get the printers going as soon as possible, start by identifying **which updates are already in the right order**.

In the above example, the first update (`75,47,61,53,29`) is in the right order:

* `75` is correctly first because there are rules that put each other page after it: `75|47`, `75|61`, `75|53`, and `75|29`.
* `47` is correctly second because 75 must be before it (`75|47`) and every other page must be after it according to `47|61`, `47|53`, and `47|29`.
* `61` is correctly in the middle because 75 and 47 are before it (`75|61` and `47|61`) and 53 and 29 are after it (`61|53` and `61|29`).
* `53` is correctly fourth because it is before page number 29 (`53|29`).
* `29` is the only page left and so is correctly last.

Because the first update does not include some page numbers, the ordering rules involving those missing page numbers are ignored.

The second and third updates are also in the correct order according to the rules. Like the first update, they also do not include every page number, and so only some of the ordering rules apply - within each update, the ordering rules that involve missing page numbers are not used.

The fourth update, `75,97,47,61,53`, is **not** in the correct order: it would print 75 before 97, which violates the rule `97|75`.

The fifth update, `61,13,29`, is also **not** in the correct order, since it breaks the rule `29|13`.

The last update, `97,13,75,29,47`, is **not** in the correct order due to breaking several rules.

For some reason, the Elves also need to know the **middle page number** of each update being printed. Because you are currently only printing the correctly-ordered updates, you will need to find the middle page number of each correctly-ordered update. In the above example, the correctly-ordered updates are:

```
75,47,**61**,53,29
97,61,**53**,29,13
75,**29**,13
```

These have middle page numbers of `61`, `53`, and `29` respectively. Adding these page numbers together gives **`143`**.

Of course, you'll need to be careful: the actual list of **page ordering rules** is bigger and more complicated than the above example.

Determine which updates are already in the correct order. **What do you get if you add up the middle page number from those correctly-ordered updates?**

### --- Part Two ---

While the Elves get to work printing the correctly-ordered updates, you have a little time to fix the rest of them.

For each of the **incorrectly-ordered updates**, use the page ordering rules to put the page numbers in the right order. For the above example, here are the three incorrectly-ordered updates and their correct orderings:

* `75,97,47,61,53` becomes `97,75,**47**,61,53`.
* `61,13,29` becomes `61,**29**,13`.
* `97,13,75,29,47` becomes `97,75,**47**,29,13`.

After taking **only the incorrectly-ordered updates** and ordering them correctly, their middle page numbers are `47`, `29`, and `47`. Adding these together produces **`123`**.

Find the updates which are not in the correct order. **What do you get if you add up the middle page numbers after correctly ordering just those updates?**

## Puzzle

<!-- livebook:{"attrs":"eyJhc3NpZ25fdG8iOiJwdXp6bGVfaW5wdXQiLCJkYXkiOiI1Iiwic2Vzc2lvbl9zZWNyZXQiOiJBT0NfU0VTU0lPTiIsInllYXIiOiIyMDI0In0","chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2024", "5", System.fetch_env!("LB_AOC_SESSION"))
```

```elixir
IO.puts(puzzle_input)
```

## Parser

### Code - Parser

```elixir
defmodule Parser do
  def parse(input) do
    [ordering_rules, page_numbers] =
      input
      |> String.split("\n\n", trim: true)

    ordering_rules =
      ordering_rules
      |> String.split("\n", trim: true)
      |> Enum.map(&String.split(&1, "|", trim: true))
      |> Enum.map(&Enum.map(&1, fn num -> String.to_integer(num) end))

    page_numbers =
      page_numbers
      |> String.split("\n", trim: true)
      |> Enum.map(&String.split(&1, ",", trim: true))
      |> Enum.map(&Enum.map(&1, fn num -> String.to_integer(num) end))

    {ordering_rules, page_numbers}
  end
end
```

### Tests - Parser

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule ParserTest do
  use ExUnit.Case, async: true
  import Parser

  @input """
  47|53
  97|13
  97|61
  97|47
  75|29
  61|13
  75|53
  29|13
  97|29
  53|29
  61|53
  97|53
  61|29
  47|13
  75|47
  97|75
  47|61
  75|61
  47|29
  75|13
  53|13

  75,47,61,53,29
  97,61,53,29,13
  75,29,13
  75,97,47,61,53
  61,13,29
  97,13,75,29,47
  """
  @expected {
    [
      [47, 53],
      [97, 13],
      [97, 61],
      [97, 47],
      [75, 29],
      [61, 13],
      [75, 53],
      [29, 13],
      [97, 29],
      [53, 29],
      [61, 53],
      [97, 53],
      [61, 29],
      [47, 13],
      [75, 47],
      [97, 75],
      [47, 61],
      [75, 61],
      [47, 29],
      [75, 13],
      [53, 13]
    ],
    [
      [75, 47, 61, 53, 29],
      [97, 61, 53, 29, 13],
      [75, 29, 13],
      [75, 97, 47, 61, 53],
      [61, 13, 29],
      [97, 13, 75, 29, 47]
    ]
  }

  test "parse test" do
    assert parse(@input) == @expected
  end
end

ExUnit.run()
```

<!-- livebook:{"branch_parent_index":2} -->

## Part One

### Code - Part 1

```elixir
defmodule PartOne do
  def solve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    {rules, pages} = input |> Parser.parse()

    good_wrong_pages =
      pages
      |> Enum.map(fn page ->
        page
        |> Enum.chunk_every(2, 1, :discard)
        |> Enum.map(fn pair ->
          rules
          |> Enum.any?(&(&1 == pair))
        end)
        |> Enum.all?(& &1)
      end)

    # Filter wrong pages and calculate the sum of middle page number value
    Enum.zip(pages, good_wrong_pages)
    |> Enum.filter(fn {_, ok?} -> ok? end)
    |> Enum.map(fn {page, _} ->
      len = length(page)

      page
      |> Enum.at(Integer.floor_div(len, 2))
    end)
    |> Enum.sum()
  end
end
```

### Tests - Part 1

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input """
  47|53
  97|13
  97|61
  97|47
  75|29
  61|13
  75|53
  29|13
  97|29
  53|29
  61|53
  97|53
  61|29
  47|13
  75|47
  97|75
  47|61
  75|61
  47|29
  75|13
  53|13

  75,47,61,53,29
  97,61,53,29,13
  75,29,13
  75,97,47,61,53
  61,13,29
  97,13,75,29,47
  """
  @expected 143

  test "part one" do
    assert run(@input) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 1

```elixir
PartOne.solve(puzzle_input)
```

<!-- livebook:{"branch_parent_index":2} -->

## Part Two

### Code - Part 2

```elixir
defmodule PartTwo do
  def solve(input) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    {rules, pages} = input |> Parser.parse()

    good_wrong_pages =
      pages
      |> Enum.map(fn page ->
        page
        |> Enum.chunk_every(2, 1, :discard)
        |> Enum.map(fn pair ->
          rules
          |> Enum.any?(&(&1 == pair))
        end)
        |> Enum.all?(& &1)
      end)

    wrong_pages =
      Enum.zip(pages, good_wrong_pages)
      |> Enum.filter(fn {_, ok?} -> !ok? end)
      |> Enum.map(fn {page, _} -> page end)

    rules_map = rules_to_map(rules)

    wrong_pages
    |> reorder_wrong_pages(rules_map)
    |> Enum.map(fn page ->
      len = length(page)

      page
      |> Enum.at(Integer.floor_div(len, 2))
    end)
    |> Enum.sum()
  end

  # Reorder the wrong pages using the intersection between 
  # the rules_map item with the page and sorting 
  def reorder_wrong_pages(wrong_pages, rules_map) do
    wrong_pages
    |> Enum.map(fn page ->
      page
      |> Enum.reduce(%{}, fn item, acc ->
        case Map.get(rules_map, item) do
          nil -> Map.put(acc, item, MapSet.new())
          val -> Map.put(acc, item, MapSet.intersection(MapSet.new(val), MapSet.new(page)))
        end
      end)
      |> Map.to_list()
      |> Enum.sort(fn {_, set1}, {_, set2} -> MapSet.size(set1) <= MapSet.size(set2) end)
      |> Enum.reduce([], fn {item, _}, acc ->
        [item | acc]
      end)
    end)
  end

  # Generates a Map with the rules
  def rules_to_map(rules) do
    rules
    |> Enum.reduce(%{}, fn rule, acc ->
      key = Enum.at(rule, 0)
      value = Enum.at(rule, 1)

      case Map.has_key?(acc, key) do
        true ->
          Map.update!(acc, key, fn list ->
            [value | list]
          end)

        false ->
          Map.put(acc, key, [value])
      end
    end)
  end
end
```

### Tests - Part 2

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule PartTwoTest do
  use ExUnit.Case, async: true
  import PartTwo

  @input """
  47|53
  97|13
  97|61
  97|47
  75|29
  61|13
  75|53
  29|13
  97|29
  53|29
  61|53
  97|53
  61|29
  47|13
  75|47
  97|75
  47|61
  75|61
  47|29
  75|13
  53|13

  75,47,61,53,29
  97,61,53,29,13
  75,29,13
  75,97,47,61,53
  61,13,29
  97,13,75,29,47
  """
  @expected 123

  test "part two" do
    assert run(@input) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 2

```elixir
PartTwo.solve(puzzle_input)
```

<!-- livebook:{"offset":11474,"stamp":{"token":"XCP.6hpPiQ5hCTMZpgKRSBMT5BYapnUK15y8Pq0IB2ge8fa9HPW-fpDbbHOWCfQsr54Stn90_Tl96wHTCHFc9cjgYnZD0opKybkgbKj-wrt09hvugEr_jiQ","version":2}} -->
