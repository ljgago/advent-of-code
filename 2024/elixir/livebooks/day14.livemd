<!-- livebook:{"autosave_interval_s":30} -->

# Advent of Code 2024 - Day 14

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1"}
])
```

## Introduction

### --- Day 14: Restroom Redoubt ---

One of The Historians needs to use the bathroom; fortunately, you know there's a bathroom near an unvisited location on their list, and so you're all quickly teleported directly to the lobby of Easter Bunny Headquarters.

Unfortunately, EBHQ seems to have "improved" bathroom security **again** after your last [visit](https://adventofcode.com/2016/day/2). The area outside the bathroom is swarming with robots!

To get The Historian safely to the bathroom, you'll need a way to predict where the robots will be in the future. Fortunately, they all seem to be moving on the tile floor in predictable **straight lines**.

You make a list (your puzzle input) of all of the robots' current **positions** (`p`) and **velocities** (`v`), one robot per line. For example:

```
p=0,4 v=3,-3
p=6,3 v=-1,-3
p=10,3 v=-1,2
p=2,0 v=2,-1
p=0,0 v=1,3
p=3,0 v=-2,-2
p=7,6 v=-1,-3
p=3,0 v=-1,-2
p=9,3 v=2,3
p=7,3 v=-1,2
p=2,4 v=2,-3
p=9,5 v=-3,-3
```

Each robot's position is given as `p=x,y` where `x` represents the number of tiles the robot is from the left wall and `y` represents the number of tiles from the top wall (when viewed from above). So, a position of `p=0,0` means the robot is all the way in the top-left corner.

Each robot's velocity is given as `v=x,y` where `x` and `y` are given in **tiles per second**. Positive `x` means the robot is moving to the **right**, and positive `y` means the robot is moving **down**. So, a velocity of `v=1,-2` means that each second, the robot moves `1` tile to the right and `2` tiles up.

The robots outside the actual bathroom are in a space which is `101` tiles wide and `103` tiles tall (when viewed from above). However, in this example, the robots are in a space which is only `11` tiles wide and `7` tiles tall.

The robots are good at navigating over/under each other (due to a combination of springs, extendable legs, and quadcopters), so they can share the same tile and don't interact with each other. Visually, the number of robots on each tile in this example looks like this:

```
1.12.......
...........
...........
......11.11
1.1........
.........1.
.......1...
```

These robots have a unique feature for maximum bathroom security: they can **teleport**. When a robot would run into an edge of the space they're in, they instead **teleport to the other side**, effectively wrapping around the edges. Here is what robot `p=2,4 v=2,-3` does for the first few seconds:

```
Initial state:
...........
...........
...........
...........
..1........
...........
...........

After 1 second:
...........
....1......
...........
...........
...........
...........
...........

After 2 seconds:
...........
...........
...........
...........
...........
......1....
...........

After 3 seconds:
...........
...........
........1..
...........
...........
...........
...........

After 4 seconds:
...........
...........
...........
...........
...........
...........
..........1

After 5 seconds:
...........
...........
...........
.1.........
...........
...........
...........
```

The Historian can't wait much longer, so you don't have to simulate the robots for very long. Where will the robots be after `100` seconds?

In the above example, the number of robots on each tile after 100 seconds has elapsed looks like this:

```
......2..1.
...........
1..........
.11........
.....1.....
...12......
.1....1....
```

To determine the safest area, count the **number of robots in each quadrant** after 100 seconds. Robots that are exactly in the middle (horizontally or vertically) don't count as being in any quadrant, so the only relevant robots are:

```
..... 2..1.
..... .....
1.... .....
           
..... .....
...12 .....
.1... 1....
```

In this example, the quadrants contain `1`, `3`, `4`, and `1` robot. Multiplying these together gives a total **safety factor** of **`12`**.

Predict the motion of the robots in your list within a space which is `101` tiles wide and `103` tiles tall. **What will the safety factor be after exactly 100 seconds have elapsed?**

### --- Part Two ---

During the bathroom break, someone notices that these robots seem awfully similar to ones built and used at the North Pole. If they're the same type of robots, they should have a hard-coded Easter egg: very rarely, most of the robots should arrange themselves into **a picture of a Christmas tree**.

**What is the fewest number of seconds that must elapse for the robots to display the Easter egg?**

## Puzzle

<!-- livebook:{"attrs":"eyJhc3NpZ25fdG8iOiJwdXp6bGVfaW5wdXQiLCJkYXkiOiIxNCIsInNlc3Npb25fc2VjcmV0IjoiQU9DX1NFU1NJT04iLCJ5ZWFyIjoiMjAyNCJ9","chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2024", "14", System.fetch_env!("LB_AOC_SESSION"))
```

```elixir
IO.puts(puzzle_input)
```

## Parser

### Code - Parser

```elixir
defmodule Parser do
  @regex ~r/[-]?\d+/
  
  def parse(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(fn line ->
      Regex.scan(@regex, line)
      |> List.flatten()
      |> Enum.map(&String.to_integer(&1))
      |> (fn [p1, p2, v1, v2] -> [{p1, p2}, {v1, v2}] end).()
    end)
  end
end
```

### Tests - Parser

```elixir
ExUnit.start(autorun: false)

defmodule ParserTest do
  use ExUnit.Case, async: true
  import Parser

  @input """
  p=0,4 v=3,-3
  p=6,3 v=-1,-3
  p=10,3 v=-1,2
  p=2,0 v=2,-1
  p=0,0 v=1,3
  p=3,0 v=-2,-2
  p=7,6 v=-1,-3
  p=3,0 v=-1,-2
  p=9,3 v=2,3
  p=7,3 v=-1,2
  p=2,4 v=2,-3
  p=9,5 v=-3,-3
  """
  @expected [
    [{0, 4}, {3, -3}],
    [{6, 3}, {-1, -3}],
    [{10, 3}, {-1, 2}],
    [{2, 0}, {2, -1}],
    [{0, 0}, {1, 3}],
    [{3, 0}, {-2, -2}],
    [{7, 6}, {-1, -3}],
    [{3, 0}, {-1, -2}],
    [{9, 3}, {2, 3}],
    [{7, 3}, {-1, 2}],
    [{2, 4}, {2, -3}],
    [{9, 5}, {-3, -3}]
  ]

  test "parse test" do
    assert parse(@input) == @expected
  end
end

ExUnit.run()
```

<!-- livebook:{"branch_parent_index":2} -->

## Part One

### Code - Part 1

```elixir
defmodule PartOne do
  def solve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input, 101, 103)}")
  end

  def run(input, wide, tall) do
    initial_robots = Parser.parse(input)

    1..100
    |> Enum.reduce(initial_robots, fn _, robots ->
      robots
      |> Enum.map(fn robot ->
        compute_teleport_position(robot, wide, tall)
      end)
    end)
    |> compute_robots_by_quadrant(wide, tall)
    |> (fn [q1, q2, q3, q4] -> q1 * q2 * q3 * q4 end).()
  end

  def compute_teleport_position([{px, py}, {vx, vy}], wide, tall) do
    new_px = Integer.mod(px + vx, wide)
    new_py = Integer.mod(py + vy, tall)

    [{new_px, new_py}, {vx, vy}]
  end

  def compute_robots_by_quadrant(robots, wide, tall) do
    wide_end = div(wide, 2)
    wide_start = div(wide, 2) + 1
    tall_end = div(tall, 2)
    tall_start = div(tall, 2) + 1

    robots
    |> Enum.reduce([0, 0, 0, 0], fn [{px, py}, _], [q1, q2, q3, q4] ->
      cond do
        # quad 1
        # xo
        # oo
        px >= 0 and px < wide_end and py >= 0 and py < tall_end ->
          [q1 + 1, q2, q3, q4]

        # quad 2
        # ox
        # oo
        px >= wide_start and px < wide and py >= 0 and py < tall_end ->
          [q1, q2 + 1, q3, q4]

        # quad 3
        # oo
        # xo
        px >= 0 and px < wide_end and py >= tall_start and py < tall ->
          [q1, q2, q3 + 1, q4]

        # quad 4
        # oo
        # ox
        px >= wide_start and px < wide and py >= tall_start and py < tall ->
          [q1, q2, q3, q4 + 1]

        true ->
          [q1, q2, q3, q4]
      end
    end)
  end
end
```

### Tests - Part 1

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input """
  p=0,4 v=3,-3
  p=6,3 v=-1,-3
  p=10,3 v=-1,2
  p=2,0 v=2,-1
  p=0,0 v=1,3
  p=3,0 v=-2,-2
  p=7,6 v=-1,-3
  p=3,0 v=-1,-2
  p=9,3 v=2,3
  p=7,3 v=-1,2
  p=2,4 v=2,-3
  p=9,5 v=-3,-3
  """
  @expected 12

  test "part one" do
    assert run(@input, 11, 7) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 1

```elixir
PartOne.solve(puzzle_input)
```

<!-- livebook:{"branch_parent_index":2} -->

## Part Two

### Code - Part 2

```elixir
defmodule PartTwo do
  def solve(input, range) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input, 101, 103, range)}")
  end

  def run(input, wide, tall, range) do
    initial_robots = Parser.parse(input)

    range
    |> Enum.reduce_while(initial_robots, fn times, robots ->
      robots =
        robots
        |> Enum.map(fn robot ->
          compute_teleport_position(robot, wide, tall)
        end)

      case has_tree?(robots, wide, tall) do
        true -> {:halt, times}
        false -> {:cont, robots}
      end
    end)
  end

  def compute_teleport_position([{px, py}, {vx, vy}], wide, tall) do
    new_px = Integer.mod(px + vx, wide)
    new_py = Integer.mod(py + vy, tall)

    [{new_px, new_py}, {vx, vy}]
  end

  def has_tree?(robots, wide, tall) do
    points =
      robots
      |> Enum.map(fn [p, _] -> p end)
      |> MapSet.new()

    for y <- 0..(tall - 1) do
      for x <- 0..(wide - 1), into: "" do
        if MapSet.member?(points, {x, y}) do
          "#"
        else
          "."
        end
      end
    end
    |> Enum.any?(fn line ->
      line
      |> String.contains?("###############################")
    end)
  end
end
```

### Solution - Part 2

```elixir
PartTwo.solve(puzzle_input, 1..10000)
```

<!-- livebook:{"offset":9746,"stamp":{"token":"XCP.ajreOZ9cmceBhYsFmuxByRuBqxGKkh7MyTwU5tiv6a8WxFQLUohvNlC1Zl6zVci-fA4GPkWIRHlDybn-zKEcnQexVQ0dUnPvheN-8NnFj0Izf8BRtyM","version":2}} -->
