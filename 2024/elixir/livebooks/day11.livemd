<!-- livebook:{"autosave_interval_s":30} -->

# Advent of Code 2024 - Day 11

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1"}
])
```

## Introduction

### --- Day 11: Plutonian Pebbles ---

The ancient civilization on [Pluto](https://adventofcode.com/2019/day/20) was known for its ability to manipulate spacetime, and while The Historians explore their infinite corridors, you've noticed a strange set of physics-defying stones.

At first glance, they seem like normal stones: they're arranged in a perfectly **straight line**, and each stone has a **number** engraved on it.

The strange part is that every time you blink, the stones change.

Sometimes, the number engraved on a stone changes. Other times, a stone might split in two, causing all the other stones to shift over a bit to make room in their perfectly straight line.

As you observe them for a while, you find that the stones have a consistent behavior. Every time you blink, the stones each **simultaneously** change according to the **first applicable rule** in this list:

* If the stone is engraved with the number `0`, it is replaced by a stone engraved with the number `1`.
* If the stone is engraved with a number that has an **even** number of digits, it is replaced by **two stones**. The left half of the digits are engraved on the new left stone, and the right half of the digits are engraved on the new right stone. (The new numbers don't keep extra leading zeroes: `1000` would become stones `10` and `0`.)
* If none of the other rules apply, the stone is replaced by a new stone; the old stone's number **multiplied by 2024** is engraved on the new stone.

No matter how the stones change, their **order is preserved**, and they stay on their perfectly straight line.

How will the stones evolve if you keep blinking at them? You take a note of the number engraved on each stone in the line (your puzzle input).

If you have an arrangement of five stones engraved with the numbers `0 1 10 99 999` and you blink once, the stones transform as follows:

* The first stone, `0`, becomes a stone marked `1`.
* The second stone, `1`, is multiplied by `2024` to become `2024`.
* The third stone, `10`, is split into a stone marked `1` followed by a stone marked `0`.
* The fourth stone, `99`, is split into two stones marked `9`.
* The fifth stone, `999`, is replaced by a stone marked `2021976`.

So, after blinking once, your five stones would become an arrangement of seven stones engraved with the numbers `1 2024 1 0 9 9 2021976`.

Here is a longer example:

```
Initial arrangement:
125 17

After 1 blink:
253000 1 7

After 2 blinks:
253 0 2024 14168

After 3 blinks:
512072 1 20 24 28676032

After 4 blinks:
512 72 2024 2 0 2 4 2867 6032

After 5 blinks:
1036288 7 2 20 24 4048 1 4048 8096 28 67 60 32

After 6 blinks:
2097446912 14168 4048 2 0 2 4 40 48 2024 40 48 80 96 2 8 6 7 6 0 3 2
```

In this example, after blinking six times, you would have `22` stones. After blinking `25` times, you would have **`55312`** stones!

Consider the arrangement of stones in front of you. **How many stones will you have after blinking 25 times?**

### --- Part Two ---

The Historians sure are taking a long time. To be fair, the infinite corridors **are** very large.

**How many stones would you have after blinking a total of 75 times?**

## Puzzle

<!-- livebook:{"attrs":"eyJhc3NpZ25fdG8iOiJwdXp6bGVfaW5wdXQiLCJkYXkiOiIxMSIsInNlc3Npb25fc2VjcmV0IjoiQU9DX1NFU1NJT04iLCJ5ZWFyIjoiMjAyNCJ9","chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2024", "11", System.fetch_env!("LB_AOC_SESSION"))
```

```elixir
IO.puts(puzzle_input)
```

<!-- livebook:{"branch_parent_index":1} -->

## Part One

### Code - Part 1

```elixir
defmodule PartOne do
  require Integer

  def solve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input, 25)}")
  end

  def run(input, blinks) do
    1..blinks
    |> Enum.reduce(input, fn _, stones ->
      stones
      |> String.split(" ", trim: true)
      |> Enum.map(fn stone ->
        cond do
          stone == "0" ->
            "1"

          has_even_digits(stone) ->
            split_stone(stone)

          true ->
            multiply_by_2024(stone)
        end
      end)
      |> Enum.join(" ")
    end)
    |> String.split(" ", trim: true)
    |> Enum.count()
  end

  def has_even_digits(stone) do
    stone
    |> String.length()
    |> Integer.is_even()
  end

  def split_stone(stone) do
    stone
    |> String.split_at(stone |> String.length() |> div(2))
    |> (fn {left, right} ->
          right =
            right
            |> String.to_integer()
            |> Integer.to_string()

          Enum.join([left, right], " ")
        end).()
  end

  def multiply_by_2024(stone) do
    stone
    |> String.to_integer()
    |> Kernel.*(2024)
    |> Integer.to_string()
  end
end
```

### Tests - Part 1

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input "125 17"
  @expected 3

  test "part one blink 1" do
    assert run(@input, 1) == @expected
  end
    
  @input "125 17"
  @expected 22

  test "part one blink 6" do
    assert run(@input, 6) == @expected
  end

  @input "125 17"
  @expected 55312

  test "part one blink 25" do
    assert run(@input, 25) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 1

```elixir
PartOne.solve(puzzle_input)
```

<!-- livebook:{"branch_parent_index":1} -->

## Part Two

### Code - Part 2

```elixir
defmodule PartTwo do
  require Integer

  def solve(input) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input, 75)}")
  end

  # now, I use a map to store all stones in the reduce
  def run(input, blinks) do
    stones_map =
      input
      |> String.split(" ", trim: true)
      |> Enum.reduce(%{}, fn stone, stones ->
        stones
        |> Map.put(stone, 1)
      end)

    1..blinks
    |> Enum.reduce(stones_map, fn _, stones ->
      stones
      |> Enum.map(fn {stone, amount} ->
        cond do
          stone == "0" ->
            {"1", amount}

          has_even_digits(stone) ->
            {left, right} = split_stone(stone)
            [{left, amount}, {right, amount}]

          true ->
            {multiply_by_2024(stone), amount}
        end
      end)
      |> List.flatten()
      |> Enum.reduce(%{}, fn {stone, amount}, stones ->
        stones
        |> Map.update(stone, amount, &(&1 + amount))
      end)
    end)
    |> Enum.map(fn {_, amount} -> amount end)
    |> Enum.sum()
  end

  def has_even_digits(stone) do
    stone
    |> String.length()
    |> Integer.is_even()
  end

  def split_stone(stone) do
    stone
    |> String.split_at(stone |> String.length() |> div(2))
    |> (fn {left, right} ->
          right =
            right
            |> String.to_integer()
            |> Integer.to_string()

          {left, right}
        end).()
  end

  def multiply_by_2024(stone) do
    stone
    |> String.to_integer()
    |> Kernel.*(2024)
    |> Integer.to_string()
  end
end
```

### Tests - Part 2

```elixir
ExUnit.start(autorun: false)

defmodule PartTwoTest do
  use ExUnit.Case, async: true
  import PartTwo

  @input "125 17"
  @expected 3

  test "part two blink 1" do
    assert run(@input, 1) == @expected
  end

  @input "125 17"
  @expected 22

  test "part two blink 6" do
    assert run(@input, 6) == @expected
  end

  @input "125 17"
  @expected 55312

  test "part two blink 25" do
    assert run(@input, 25) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 2

```elixir
PartTwo.solve(puzzle_input)
```

<!-- livebook:{"offset":7661,"stamp":{"token":"XCP.h-CK_C-PFHdCKUwAD1JJyHzNKdqPkslK1wSh2tiSCChIs5TSC_jEBJxbM0JIAcwgZOIA-tFNOUgMNsHMd6Ux48kByeLX7_sHGpJvDorfq0qZTOGLOzc","version":2}} -->
