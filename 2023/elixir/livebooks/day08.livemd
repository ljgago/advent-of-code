<!-- livebook:{"autosave_interval_s":30} -->

# Advent of Code 2023 - Day 8

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1"}
])
```

## Introduction

### --- Day 8: Haunted Wasteland ---

You're still riding a camel across Desert Island when you spot a sandstorm quickly approaching. When you turn to warn the Elf, she disappears before your eyes! To be fair, she had just finished warning you about **ghosts** a few minutes ago.

One of the camel's pouches is labeled "maps" - sure enough, it's full of documents (your puzzle input) about how to navigate the desert. At least, you're pretty sure that's what they are; one of the documents contains a list of left/right instructions, and the rest of the documents seem to describe some kind of **network** of labeled nodes.

It seems like you're meant to use the **left/right** instructions to **navigate the network**. Perhaps if you have the camel follow the same instructions, you can escape the haunted wasteland!

After examining the maps for a bit, two nodes stick out: `AAA` and `ZZZ`. You feel like `AAA` is where you are now, and you have to follow the left/right instructions until you reach `ZZZ`.

This format defines each **node** of the network individually. For example:

```
RL

AAA = (BBB, CCC)
BBB = (DDD, EEE)
CCC = (ZZZ, GGG)
DDD = (DDD, DDD)
EEE = (EEE, EEE)
GGG = (GGG, GGG)
ZZZ = (ZZZ, ZZZ)
```

Starting with `AAA`, you need to **look up the next element** based on the next left/right instruction in your input. In this example, start with `AAA` and go **right** (`R`) by choosing the right element of `AAA`, **`CCC`**. Then, L means to choose the **left** element of `CCC`, **`ZZZ`**. By following the left/right instructions, you reach `ZZZ` in **`2`** steps.

Of course, you might not find `ZZZ` right away. If you run out of left/right instructions, repeat the whole sequence of instructions as necessary: `RL` really means `RLRLRLRLRLRLRLRL...` and so on. For example, here is a situation that takes **`6`** steps to reach `ZZZ`:

```
LLR

AAA = (BBB, BBB)
BBB = (AAA, ZZZ)
ZZZ = (ZZZ, ZZZ)
```

Starting at `AAA`, follow the left/right instructions. **How many steps are required to reach `ZZZ`?**

### --- Part Two ---

The sandstorm is upon you and you aren't any closer to escaping the wasteland. You had the camel follow the instructions, but you've barely left your starting position. It's going to take **significantly more steps** to escape!

What if the map isn't for people - what if the map is for **ghosts**? Are ghosts even bound by the laws of spacetime? Only one way to find out.

After examining the maps a bit longer, your attention is drawn to a curious fact: the number of nodes with names ending in `A` is equal to the number ending in `Z`! If you were a ghost, you'd probably just **start at every node that ends with `A`** and follow all of the paths at the same time until they all simultaneously end up at nodes that end with `Z`.

For example:

```
LR

11A = (11B, XXX)
11B = (XXX, 11Z)
11Z = (11B, XXX)
22A = (22B, XXX)
22B = (22C, 22C)
22C = (22Z, 22Z)
22Z = (22B, 22B)
XXX = (XXX, XXX)
```

Here, there are two starting nodes, `11A` and `22A` (because they both end with `A`). As you follow each left/right instruction, use that instruction to **simultaneously** navigate away from both nodes you're currently on. Repeat this process until **all** of the nodes you're currently on end with `Z`. (If only some of the nodes you're on end with `Z`, they act like any other node and you continue as normal.) In this example, you would proceed as follows:

* Step 0: You are at `11A` and `22A`.
* Step 1: You choose all of the **left** paths, leading you to `11B` and `22B`.
* Step 2: You choose all of the **right** paths, leading you to **`11Z`** and `22C`.
* Step 3: You choose all of the **left** paths, leading you to `11B` and **`22Z`**.
* Step 4: You choose all of the **right** paths, leading you to **`11Z`** and `22B`.
* Step 5: You choose all of the **left** paths, leading you to `11B` and `22C`.
* Step 6: You choose all of the **right** paths, leading you to **`11Z`** and **`22Z`**.

So, in this example, you end up entirely on nodes that end in `Z` after **6** steps.

Simultaneously start on every node that ends with `A`. **How many steps does it take before you're only on nodes that end with `Z`?**

## Puzzle

<!-- livebook:{"attrs":{"assign_to":"puzzle_input","day":"8","session_secret":"AOC_SESSION","year":"2023"},"chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2023", "8", System.fetch_env!("LB_AOC_SESSION"))
```

```elixir
IO.puts(puzzle_input)
```

## Parser

### Code - Parser

```elixir
defmodule Parser do
  def parse(input) do
    [directions, nodes] =
      input
      |> String.split("\n\n", trim: true)

    nodes =
      nodes
      |> String.split("\n", trim: true)
      |> Enum.map(fn node ->
        [key, val] = node |> String.split(" = ", trim: true)

        val =
          val
          |> String.split(["(", ", ", ")"], trim: true)
          |> List.to_tuple()

        {key, val}
      end)
      |> Map.new()

    [directions, nodes]
  end
end
```

### Tests - Parser

```elixir
ExUnit.start(autorun: false)

defmodule ParserTest do
  use ExUnit.Case, async: true
  import Parser

  @input """
  RL

  AAA = (BBB, CCC)
  BBB = (DDD, EEE)
  CCC = (ZZZ, GGG)
  DDD = (DDD, DDD)
  EEE = (EEE, EEE)
  GGG = (GGG, GGG)
  ZZZ = (ZZZ, ZZZ)
  """
  @expected [
    "RL",
    %{
      "AAA" => {"BBB", "CCC"},
      "BBB" => {"DDD", "EEE"},
      "CCC" => {"ZZZ", "GGG"},
      "DDD" => {"DDD", "DDD"},
      "EEE" => {"EEE", "EEE"},
      "GGG" => {"GGG", "GGG"},
      "ZZZ" => {"ZZZ", "ZZZ"}
    }
  ]

  test "parse test" do
    assert parse(@input) == @expected
  end
end

ExUnit.run()
```

<!-- livebook:{"branch_parent_index":2} -->

## Part One

### Code - Part 1

```elixir
defmodule PartOne do
  def solve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    [directions, nodes] = input |> Parser.parse()
    navigate(directions, nodes)
  end

  defp navigate(directions, nodes), do: navigate(directions, nodes, 0, "AAA")
  defp navigate(_, _, step, "ZZZ"), do: step

  defp navigate(directions, nodes, step, node) do
    dir = get_direction(directions, step)
    {left, right} = nodes[node]

    case dir do
      "L" -> navigate(directions, nodes, step + 1, left)
      _ -> navigate(directions, nodes, step + 1, right)
    end
  end

  defp get_direction(directions, step) do
    String.at(directions, Integer.mod(step, String.length(directions)))
  end
end
```

### Tests - Part 1

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input1 """
  RL

  AAA = (BBB, CCC)
  BBB = (DDD, EEE)
  CCC = (ZZZ, GGG)
  DDD = (DDD, DDD)
  EEE = (EEE, EEE)
  GGG = (GGG, GGG)
  ZZZ = (ZZZ, ZZZ)
  """
  @expected1 2

  @input2 """
  LLR

  AAA = (BBB, BBB)
  BBB = (AAA, ZZZ)
  ZZZ = (ZZZ, ZZZ)
  """
  @expected2 6

  test "part one" do
    assert run(@input1) == @expected1
    assert run(@input2) == @expected2
  end
end

ExUnit.run()
```

### Solution - Part 1

```elixir
PartOne.solve(puzzle_input)
```

<!-- livebook:{"branch_parent_index":2} -->

## Part Two

### Code - Part 2

```elixir
defmodule PartTwo do
  def solve(input) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    [directions, nodes] = input |> Parser.parse()

    navigate(directions, nodes)
  end

  defp navigate(directions, nodes) do
    nodes
    |> Map.keys()
    |> Enum.filter(&(String.last(&1) == "A"))
    |> Enum.map(fn node ->
      navigate(directions, nodes, 0, node)
    end)
    |> Enum.reduce(&lcm/2)
  end

  defp navigate(_, _, step, <<_::utf8, _::utf8, "Z">>), do: step

  defp navigate(directions, nodes, step, node) do
    dir = get_direction(directions, step)
    {left, right} = nodes[node]

    case dir do
      "L" -> navigate(directions, nodes, step + 1, left)
      _ -> navigate(directions, nodes, step + 1, right)
    end
  end

  defp get_direction(directions, step) do
    String.at(directions, Integer.mod(step, String.length(directions)))
  end

  # Least common multiple of 2 numbers
  defp lcm(a, b) do
    div(a * b, Integer.gcd(a, b))
  end
end
```

### Tests - Part 2

```elixir
ExUnit.start(autorun: false)

defmodule PartTwoTest do
  use ExUnit.Case, async: true
  import PartTwo

  @input """
  LR

  11A = (11B, XXX)
  11B = (XXX, 11Z)
  11Z = (11B, XXX)
  22A = (22B, XXX)
  22B = (22C, 22C)
  22C = (22Z, 22Z)
  22Z = (22B, 22B)
  XXX = (XXX, XXX)
  """
  @expected 6

  test "part two" do
    assert run(@input) == @expected
  end
end

ExUnit.run()
```

### Solution - Part 2

```elixir
PartTwo.solve(puzzle_input)
```

<!-- livebook:{"offset":8856,"stamp":{"token":"XCP.GK5xUr-bRDd39-tuxbncGCRIcpnhOqdBTGVh4GkwjF6to2k-kclK88vSE5NoePeYrZxt8lWlkbHPHSzONyeMWvmDx5WwO9cEXZIHRcpxMOM7IS7lyU-U","version":2}} -->
