<!-- livebook:{"autosave_interval_s":30} -->

# Advent of Code 2023 - Day 2

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1"}
])
```

## Introduction

### --- Day 2: Cube Conundrum ---

You're launched high into the atmosphere! The apex of your trajectory just barely reaches the surface of a large island floating in the sky. You gently land in a fluffy pile of leaves. It's quite cold, but you don't see much snow. An Elf runs over to greet you.

The Elf explains that you've arrived at **Snow Island** and apologizes for the lack of snow. He'll be happy to explain the situation, but it's a bit of a walk, so you have some time. They don't get many visitors up here; would you like to play a game in the meantime?

As you walk, the Elf shows you a small bag and some cubes which are either red, green, or blue. Each time you play this game, he will hide a secret number of cubes of each color in the bag, and your goal is to figure out information about the number of cubes.

To get information, once a bag has been loaded with cubes, the Elf will reach into the bag, grab a handful of random cubes, show them to you, and then put them back in the bag. He'll do this a few times per game.

You play several games and record the information from each game (your puzzle input). Each game is listed with its ID number (like the `11` in `Game 11: ...`) followed by a semicolon-separated list of subsets of cubes that were revealed from the bag (like `3 red, 5 green, 4 blue`).

For example, the record of a few games might look like this:

```
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
```

In game 1, three sets of cubes are revealed from the bag (and then put back again). The first set is 3 blue cubes and 4 red cubes; the second set is 1 red cube, 2 green cubes, and 6 blue cubes; the third set is only 2 green cubes.

The Elf would first like to know which games would have been possible if the bag contained **only 12 red cubes, 13 green cubes, and 14 blue cubes?**

In the example above, games 1, 2, and 5 would have been possible if the bag had been loaded with that configuration. However, game 3 would have been **impossible** because at one point the Elf showed you 20 red cubes at once; similarly, game 4 would also have been impossible because the Elf showed you 15 blue cubes at once. If you add up the IDs of the games that would have been possible, you get **`8`**.

Determine which games would have been possible if the bag had been loaded with only 12 red cubes, 13 green cubes, and 14 blue cubes. **What is the sum of the IDs of those games?**

### --- Part Two ---

The Elf says they've stopped producing snow because they aren't getting any **water**! He isn't sure why the water stopped; however, he can show you how to get to the water source to check it out for yourself. It's just up ahead!

As you continue your walk, the Elf poses a second question: in each game you played, what is the **fewest number of cubes of each color** that could have been in the bag to make the game possible?

Again consider the example games from earlier:

```
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
```

* In game 1, the game could have been played with as few as 4 red, 2 green, and 6 blue cubes. If any color had even one fewer cube, the game would have been impossible.
* Game 2 could have been played with a minimum of 1 red, 3 green, and 4 blue cubes.
* Game 3 must have been played with at least 20 red, 13 green, and 6 blue cubes.
* Game 4 required at least 14 red, 3 green, and 15 blue cubes.
* Game 5 needed no fewer than 6 red, 3 green, and 2 blue cubes in the bag.

The **power** of a set of cubes is equal to the numbers of red, green, and blue cubes multiplied together. The power of the minimum set of cubes in game 1 is `48`. In games 2-5 it was `12`, `1560`, `630`, and `36`, respectively. Adding up these five powers produces the sum **`2286`**.

For each game, find the minimum set of cubes that must have been present. **What is the sum of the power of these sets?**

## Puzzle

<!-- livebook:{"attrs":{"assign_to":"puzzle_input","day":"2","session_secret":"AOC_SESSION","year":"2023"},"chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2023", "2", System.fetch_env!("LB_AOC_SESSION"))
```

## Parser

### Code - Parser

```elixir
defmodule Cube do
  defstruct red: 0, green: 0, blue: 0
end

defmodule Game do
  defstruct id: 0, sets: [Cube]
end

defmodule Parser do
  def parse(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_game(&1))
  end

  defp parse_game(game) do
    [game_id | game_sets] = game |> String.split([": ", "; "], trim: true)

    id =
      game_id
      |> String.split("Game ", trim: true)
      |> List.first()
      |> String.to_integer()

    sets = parse_sets(game_sets)

    %Game{id: id, sets: sets}
  end

  defp parse_sets(sets) do
    sets
    |> Enum.map(&parse_cubes(&1))
  end

  defp parse_cubes(cubes) do
    cubes
    |> String.split(", ", trim: true)
    |> Enum.reduce(%Cube{}, fn cube, acc ->
      case parse_cube(cube) do
        {num, "red"} -> %Cube{acc | red: num}
        {num, "green"} -> %Cube{acc | green: num}
        {num, "blue"} -> %Cube{acc | blue: num}
        _ -> acc
      end
    end)
  end

  defp parse_cube(cube) do
    [num, color] = cube |> String.split(" ", trim: true)
    {String.to_integer(num), color}
  end
end
```

### Tests - Parser

```elixir
ExUnit.start(autorun: false)

defmodule ParserTest do
  use ExUnit.Case, async: true
  import Parser

  @input """
  Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
  Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
  Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
  Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
  Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
  """

  @expected [
    %Game{
      id: 1,
      sets: [
        %Cube{red: 4, green: 0, blue: 3},
        %Cube{red: 1, green: 2, blue: 6},
        %Cube{red: 0, green: 2, blue: 0}
      ]
    },
    %Game{
      id: 2,
      sets: [
        %Cube{red: 0, green: 2, blue: 1},
        %Cube{red: 1, green: 3, blue: 4},
        %Cube{red: 0, green: 1, blue: 1}
      ]
    },
    %Game{
      id: 3,
      sets: [
        %Cube{red: 20, green: 8, blue: 6},
        %Cube{red: 4, green: 13, blue: 5},
        %Cube{red: 1, green: 5, blue: 0}
      ]
    },
    %Game{
      id: 4,
      sets: [
        %Cube{red: 3, green: 1, blue: 6},
        %Cube{red: 6, green: 3, blue: 0},
        %Cube{red: 14, green: 3, blue: 15}
      ]
    },
    %Game{id: 5, sets: [%Cube{red: 6, green: 3, blue: 1}, %Cube{red: 1, green: 2, blue: 2}]}
  ]

  test "parse test" do
    actual = parse(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

<!-- livebook:{"branch_parent_index":2} -->

## Part One

### Code - Part 1

```elixir
defmodule PartOne do
  def solve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input)}")
  end

  @max_cubes %Cube{red: 12, green: 13, blue: 14}

  def run(input) do
    input
    |> Parser.parse()
    |> Enum.filter(&possible?(&1, @max_cubes))
    |> Enum.map(fn game -> game.id end)
    |> Enum.sum()
  end

  defp possible?(game, max_cubes) do
    game.sets
    |> Enum.all?(fn cubes ->
      cubes.red <= max_cubes.red and cubes.green <= max_cubes.green and
        cubes.blue <= max_cubes.blue
    end)
  end
end
```

### Tests - Part 1

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input """
  Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
  Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
  Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
  Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
  Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
  """
  @expected 8

  test "part one" do
    actual = run(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

### Solution - Part 1

```elixir
PartOne.solve(puzzle_input)
```

<!-- livebook:{"branch_parent_index":2} -->

## Part Two

### Code - Part 2

```elixir
defmodule PartTwo do
  def solve(input) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    input
    |> Parser.parse()
    |> Enum.map(&fewest_color_cubes(&1))
    |> Enum.map(fn cube -> cube.red * cube.green * cube.blue end)
    |> Enum.sum()
  end

  defp fewest_color_cubes(game) do
    game.sets
    |> Enum.reduce(%Cube{red: 1, green: 1, blue: 1}, fn cube, acc ->
      %Cube{
        red: Enum.max([acc.red, cube.red]),
        green: Enum.max([acc.green, cube.green]),
        blue: Enum.max([acc.blue, cube.blue])
      }
    end)
  end
end
```

### Tests - Part 2

```elixir
ExUnit.start(autorun: false)

defmodule PartTwoTest do
  use ExUnit.Case, async: true
  import PartTwo

  @input """
  Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
  Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
  Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
  Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
  Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
  """
  @expected 2286

  test "part two" do
    actual = run(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

### Solution - Part 2

```elixir
PartTwo.solve(puzzle_input)
```

<!-- livebook:{"offset":10090,"stamp":{"token":"XCP.tmLAqwEQAiSZIbZUHDKh8woA1es_euH_odjs2d_tTiNlAhE7yh2jOmZDqOZe-E-XmOtpcL3kvoQ0plqkxLmnlzeK4p53DF7KRZbqhVvtgODN5kOM0E04","version":2}} -->
